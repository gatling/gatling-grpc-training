plugins {
  id "application"
  id "idea"
  id "java"
  id "com.google.protobuf"   version "0.9.5"
  id "com.diffplug.spotless" version "7.2.1"
}

repositories {
  mavenCentral()
}

dependencies {
  compileOnly "javax.annotation:javax.annotation-api:1.3.2"
  implementation "io.grpc:grpc-netty:1.74.0"
  implementation "io.grpc:grpc-protobuf:1.74.0"
  implementation "io.grpc:grpc-services:1.74.0" // reflection
  implementation "com.nimbusds:nimbus-jose-jwt:10.4.1"
  implementation "org.bouncycastle:bcpkix-jdk18on:1.81"
}

idea {
  module {
    generatedSourceDirs += file("${protobuf.generatedFilesBaseDir}/main/java")
    generatedSourceDirs += file("${protobuf.generatedFilesBaseDir}/main/grpc")
  }
}

protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:3.25.8"
  }
  plugins {
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:1.74.0"
    }
  }
  generateProtoTasks {
    ofSourceSet("main").forEach {
      it.plugins {
        grpc {}
      }
    }
  }
}

spotless {
  java {
    palantirJavaFormat()
    importOrder("java|javax", "scala", "io.gatling", "", "\\#")
    removeUnusedImports()
    target project.fileTree(project.rootDir) {
      include "**/*.java"
      exclude "build/generated/**/*.*"
    }
    trimTrailingWhitespace()
  }
}

application {
  mainClassName =
    project.hasProperty("mainClass")
      ? project.property("mainClass")
      : "io.gatling.grpc.training.server.calculator.CalculatorServer"

}

run {
  systemProperties System.getProperties()
}
