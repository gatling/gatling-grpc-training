plugins {
  id "application"
  id "idea"
  id "java"
  id "com.google.protobuf"   version "0.9.4"
  id "com.diffplug.spotless" version "6.25.0"
}

repositories {
  mavenCentral()
}

dependencies {
  compileOnly "javax.annotation:javax.annotation-api:1.3.2"
  implementation "io.grpc:grpc-netty:1.68.1"
  implementation "io.grpc:grpc-protobuf:1.68.1"
  implementation "io.grpc:grpc-services:1.68.1" // reflection
  implementation "com.nimbusds:nimbus-jose-jwt:9.41.2"
  implementation "org.bouncycastle:bcpkix-jdk18on:1.79"
}

idea {
  module {
    generatedSourceDirs += file("${protobuf.generatedFilesBaseDir}/main/java")
    generatedSourceDirs += file("${protobuf.generatedFilesBaseDir}/main/grpc")
  }
}

protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:3.25.5"
  }
  plugins {
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:1.68.1"
    }
  }
  generateProtoTasks {
    ofSourceSet("main").forEach {
      it.plugins {
        grpc {}
      }
    }
  }
}

spotless {
  java {
    palantirJavaFormat()
    importOrder("java|javax", "scala", "io.gatling", "", "\\#")
    removeUnusedImports()
    target project.fileTree(project.rootDir) {
      include "**/*.java"
      exclude "build/generated/**/*.*"
    }
    trimTrailingWhitespace()
  }
}

application {
  mainClassName =
    project.hasProperty("mainClass")
      ? project.property("mainClass")
      : "io.gatling.grpc.training.server.calculator.CalculatorServer"

}

run {
  systemProperties System.getProperties()
}
